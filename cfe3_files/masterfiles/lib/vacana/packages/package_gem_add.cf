bundle agent va_package_gem_add(gem, options, promisee)
# @depends va_package_gem_manage
# @brief Ensure that 'gem' is present on the system
#
# @classes va_package_gem_add_${cgem}_(kept|repaired|failed)
#
# NOTE: cgem is canonify("${gem}")
#
# @param gem        Name of the gem to add to the system
# @param options    Options passed to the gem command
# @param promisee   A promisee for this promise
{
  meta:

    "purpose" string => "Add given gem via ruby's gem package manager to the system";
    "tags"     slist => { "packages", "gem", "vacana" };

  vars:

    "cgem" string => canonify("${gem}");

  methods:

    "va_package_gem_add_${cgem}"
      comment   => "Add ${gem} to the system via ruby's gem package manager",
      handle    => "va_package_gem_add_methods_package_gem_manage",
      usebundle => va_package_gem_manage(
        "add", "${gem}", "${options}", "${promisee}"
      );
}
