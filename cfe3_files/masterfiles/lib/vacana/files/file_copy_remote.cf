bundle agent va_file_copy_remote(src, server, dest, promisee)
# @depends  va_log_promise va_classes_generic
# @brief    Copy 'src' from remote 'server' to local 'dest'
#
# @classes  va_file_copy_remote_${cdest}_(kept|repaired|failed)
#
# NOTE: cdest is canonify("${dest}")
#
# @param src      The source file on the server
# @param server   The server from which to get the 'src'
# @param dest     The destination of the file on the local host
# @param promisee A promisee for this files promise
{
  meta:
    
    "purpose" string => "Copy src from server to local dest";
    "tags"     slist => { "files", "vacana" };

  vars:

    "class_prefix"
      comment => "Prefix promise outcome classes",
      string  => canonify("va_file_copy_remote_${dest}");

  files:

    "${dest}" -> { "${promisee}" }
      comment   => "Copy ${src} from ${server} to local ${dest}",
      handle    => "va_file_copy_remote_files_copy",
      action    => va_log_promise(
        "${class_prefix}", "${this.handle}", "${this.promiser}", "${promisee}"),
      classes   => va_classes_generic("${class_prefix}"),
      copy_from => secure_cp("${src}", "${server}");
}
